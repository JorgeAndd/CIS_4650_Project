%{
	#include "y.tab.h"
	#include <stdio.h>
	
	int comment_line;
%}

%option yylineno
%x COMMENT

%%

"/*"            		{ BEGIN(COMMENT); comment_line = yylineno;}
<COMMENT>"*/" 			{ BEGIN(INITIAL); }
<COMMENT>([^*]|\n)+|.	
<COMMENT><<EOF>>		{ warning("Unterminated comment", comment_line); 
							return 0;}


[0-9]+								yylval.ival = atoi(yytext); return INT;
[0-9]+\.[0-9]+([Ee][+-]?[0-9]+)?	yylval.fval = atof(yytext); return FLOAT;
\'[a-zA-Z]\'						yylval.cval = *yytext; return CHAR;

"=="	return EQUAL;
"!="	return DIFF;
">="	return GE;
"<="	return LE;

"&&"	return AND;
"||"	return OR;
"!"		return NOT;

"="			return ASSIGN;
"sizeof"	return SIZEOF;
"typedef"	return TYPEDEF;

"int"		return TKINT;
"char"		return TKCHAR;
"float"		return TKFLOAT;

"++"	return INC;
"--"	return DEC;

"."		return DOT;
"'"		return QUOTE;
";"		return SEMICOLON;
","		return COMMA;

"{"		return OBRACES;
"}"		return CBRACES;
"("		return OPAR;
")" 	return CPAR;
"["		return OBRACKET;
"]"		return CBRACKET;

"if"		return IF;
"else"		return ELSE;
"for"		return FOR;
"while"		return WHILE;
"return"	return RETURN;
"void"		return VOID;
"struct"	return STRUCT;

[a-zA-Z_][a-zA-Z0-9]*				yylval.sval = strdup(yytext); return NAME;
\n			/*ignores new line*/
[ \t]+		/*ignores whitespace*/
.			return *yytext;	

%%
 
